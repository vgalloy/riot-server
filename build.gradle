group = 'vgalloy'
version = '1.0-SNAPSHOT'

ext {
    junitVersion = '4.12'
    riotApiJavaVersion = '3.11.0'
    slf4jApi = '1.7.22'
    logbackClassicVersion = '1.1.9'
    mongoJavaDriverVersion = '3.4.2'
    mongoJackVersion = '2.6.1'
    flapdoodleEmbedMongoVersion = '1.50.5'
    commonsConfiguration2Version = '2.1'
    commonsBeanutilsVersion = '1.9.3'
    javaOverRabbitmqVersion = '1.3.0'
    springBootVersion = '1.5.1.RELEASE'
    springfoxSwaggerVersion = '2.6.1'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'
    apply plugin: 'findbugs'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url 'http://repo.spring.io/snapshot' }
        maven { url 'http://repo.spring.io/milestone' }
    }

    checkstyle {
        toolVersion = '7.5'
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
        checkstyleTest.enabled = false
    }

    pmd {
        consoleOutput = true
        ruleSetFiles = rootProject.files('config/pmd/ruleset.xml')
    }

    findbugs {
        toolVersion = "3.0.1"
        excludeFilter = file("$rootProject.projectDir/config/findbugs/findbugs-exception-filter.xml")
    }

    test {
        testLogging {
            events "passed", "skipped", "failed", "standardOut", "standardError"
        }
        testLogging.showStandardStreams = true
    }
}

project(':riot-server-dao') {
    dependencies {
        testCompile "junit:junit:$junitVersion"
        compile "vgalloy:riot-api-java:$riotApiJavaVersion"
        compile "org.slf4j:slf4j-api:$slf4jApi"
        compile "ch.qos.logback:logback-classic:$logbackClassicVersion"
        compile "org.mongodb:mongo-java-driver:$mongoJavaDriverVersion"
        compile "org.mongojack:mongojack:$mongoJackVersion"
        compile "de.flapdoodle.embed:de.flapdoodle.embed.mongo:$flapdoodleEmbedMongoVersion"
        compile "org.apache.commons:commons-configuration2:$commonsConfiguration2Version"
        compile "commons-beanutils:commons-beanutils:$commonsBeanutilsVersion"
    }
}

project(':riot-server-loader') {
    dependencies {
        compile project(':riot-server-dao')
        compile "vgalloy:java-over-rabbitmq:$javaOverRabbitmqVersion"
    }
}

project(':riot-server-service') {
    dependencies {
        compile project(':riot-server-loader')
    }
}

project(':riot-server-webservice') {

    buildscript {
        repositories {
            jcenter()
            maven { url 'http://repo.spring.io/snapshot' }
            maven { url 'http://repo.spring.io/milestone' }
        }
        dependencies {
            classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.5.1.RELEASE'
        }
    }

    dependencies {
        compile project(':riot-server-service')
        compile "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
        compile "io.springfox:springfox-swagger-ui:$springfoxSwaggerVersion"
        compile "io.springfox:springfox-swagger2:$springfoxSwaggerVersion"
    }
    apply plugin: 'java'
    apply plugin: 'checkstyle'
//    apply plugin: 'spring-boot'
//    apply plugin: 'org.springframework.boot'
}