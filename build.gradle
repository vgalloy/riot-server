plugins {
    id 'org.springframework.boot' version "1.5.1.RELEASE" apply false
    id 'com.github.ben-manes.versions' version '0.14.0'
}

ext {
    javaVersion = '1.8'
    findbugsVersion = '3.0.1'
    checkstyleVersion = '7.5'

    junitVersion = '4.12'
    riotApiJavaVersion = '3.11.0'
    slf4jApi = '1.7.24'
    logbackClassicVersion = '1.2.1'
    mongoJavaDriverVersion = '3.4.2'
    mongoJackVersion = '2.6.1'
    flapdoodleEmbedMongoVersion = '2.0.0'
    commonsConfiguration2Version = '2.1.1'
    commonsBeanutilsVersion = '1.9.3'
    javaOverRabbitmqVersion = '1.3.0'
    springBootVersion = '1.5.1.RELEASE'
    springfoxSwaggerVersion = '2.6.1'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'
    apply plugin: 'findbugs'

    sourceCompatibility = "$javaVersion"
    targetCompatibility = "$javaVersion"
    group = 'vgalloy'
    version = '1.0-SNAPSHOT'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    checkstyle {
        toolVersion = "$checkstyleVersion"
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
        checkstyleTest.enabled = false
    }

    pmd {
        consoleOutput = true
        ruleSetFiles = rootProject.files('config/pmd/ruleset.xml')
    }

    findbugs {
        toolVersion = "$findbugsVersion"
        excludeFilter = file("$rootProject.projectDir/config/findbugs/findbugs-exception-filter.xml")
    }

    test {
        testLogging {
            events "passed", "skipped", "failed", "standardOut", "standardError"
        }
        testLogging.showStandardStreams = true
    }
}

project(':riot-server-dao') {
    dependencies {
        testCompile "junit:junit:$junitVersion"
        compile "vgalloy:riot-api-java:$riotApiJavaVersion"
        compile "org.slf4j:slf4j-api:$slf4jApi"
        compile "ch.qos.logback:logback-classic:$logbackClassicVersion"
        compile "org.mongodb:mongo-java-driver:$mongoJavaDriverVersion"
        compile "org.mongojack:mongojack:$mongoJackVersion"
        compile "de.flapdoodle.embed:de.flapdoodle.embed.mongo:$flapdoodleEmbedMongoVersion"
        compile "org.apache.commons:commons-configuration2:$commonsConfiguration2Version"
        compile "commons-beanutils:commons-beanutils:$commonsBeanutilsVersion"
    }
}

project(':riot-server-loader') {
    dependencies {
        compile project(':riot-server-dao')
        compile "vgalloy:java-over-rabbitmq:$javaOverRabbitmqVersion"
    }
}

project(':riot-server-service') {
    dependencies {
        compile project(':riot-server-loader')
    }
}

project(':riot-server-webservice') {
    apply plugin: 'org.springframework.boot'
    dependencies {
        compile project(':riot-server-service')
        compile "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
        compile "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
        compile "io.springfox:springfox-swagger-ui:$springfoxSwaggerVersion"
        compile "io.springfox:springfox-swagger2:$springfoxSwaggerVersion"
    }
}